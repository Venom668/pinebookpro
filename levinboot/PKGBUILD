pkgname=levinboot
pkgver=0.8.7
pkgrel=1
_tfaver=2.7
pkgdesc='A bootloader project for the RK3399 SoC.'
arch=('aarch64')
url='https://gitlab.com/DeltaGem/levinboot'
license=('CC0')
depends=('dtc' 'zstd')
makedepends=('git' 'ninja' 'python3')
optdepends=('mtd-utils: SPI installation support')
install=${pkgname}.install
backup=('boot/levinboot/cmdline.txt')
source=("https://gitlab.com/DeltaGem/levinboot/-/archive/v${pkgver}/levinboot-v${pkgver}.tar.gz"
        "https://git.trustedfirmware.org/TF-A/trusted-firmware-a.git/snapshot/trusted-firmware-a-$_tfaver.tar.gz"
        "levinboot.hook"
        "update-payload.sh")
sha256sums=('6e26ade0058138fb421d9b40bccb1dbe8d91702ecc138c2a855b36fb3814ce00'
            '53422dc649153838e03820330ba17cb10afe3e330ecde0db11e4d5f1361a33e6'
            '88f1c22f5dac2d3842e5999064dabf6664bc9fbed27126ee3371b9f912bec844'
            '658cbfc91cc26496ee692bc761e0ab6590c4ccf54c79573e733ad2c18773c8e2')

prepare() {
  cd "${srcdir}/levinboot-v${pkgver}"

  # Serial console 115200 baud
  sed -i "s/DCONFIG_UART_CLOCK_DIV=1/DCONFIG_UART_CLOCK_DIV=13/" configure.py

  mkdir -vp build
  cd build

  # Modify accordingly to your use case.
  CFLAGS="$CFLAGS -mno-outline-atomics" python ../configure.py \
  --with-tf-a-headers "${srcdir}/trusted-firmware-a-${_tfaver}/include/export" \
  --payload-zstd --payload-gzip --payload-lz4\
  --payload-initcpio --payload-emmc --payload-sd \
  --boards pbp
}

build() {
  unset CFLAGS CXXFLAGS CPPFLAGS LDFLAGS
  cd "${srcdir}/trusted-firmware-a-${_tfaver}"
  make PLAT=rk3399

  cd "${srcdir}/levinboot-v${pkgver}/build"
  ninja
}

package() {
  cd "${srcdir}/levinboot-v${pkgver}/build"
  mkdir -p "${pkgdir}/boot/levinboot/usb"
  cp teststage.bin memtest.bin dramstage.bin sramstage-usb.bin levinboot-usb.bin "${pkgdir}/boot/levinboot/usb/"
  cp levinboot-sd.img levinboot-spi.img "${pkgdir}/boot/levinboot/"
  cp "${srcdir}/trusted-firmware-a-${_tfaver}/build/rk3399/release/bl31/bl31.elf" "${pkgdir}/boot/levinboot/"
  cp "${srcdir}/update-payload.sh" "${pkgdir}/boot/levinboot/"
  echo "console=ttyS2,115200 root=LABEL=ROOT_ALARM rw rootwait plymouth.ignore-serial-consoles" > "${pkgdir}/boot/levinboot/cmdline.txt"
  install -Dm644 "${srcdir}/levinboot.hook" "${pkgdir}/usr/share/libalpm/hooks/levinboot.hook"
}
