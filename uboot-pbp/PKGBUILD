# U-Boot: Pinebook Pro based on PKGBUILD for Rock64
# Contributor: Dan Johansen <strit@manjaro.org>

pkgname=uboot-pbp
pkgver=2022.01
pkgrel=1
_tfaver=2.6
pkgdesc="U-Boot for Pinebook Pro"
arch=('aarch64')
url='http://www.denx.de/wiki/U-Boot/WebHome'
license=('GPL')
makedepends=('git' 'arm-none-eabi-gcc' 'dtc' 'bc' 'python3')
optdepends=('mtd-utils: SPI installation support')
provides=('uboot')
conflicts=('uboot')
install=${pkgname}.install
source=("ftp://ftp.denx.de/pub/u-boot/u-boot-${pkgver}.tar.bz2"
        "https://git.trustedfirmware.org/TF-A/trusted-firmware-a.git/snapshot/trusted-firmware-a-$_tfaver.tar.gz"
        "0001-PBP-Fix-Panel-reset.patch"
        "0002-Correct-boot-order-to-be-USB-SD-eMMC.patch"
        "0003-rk3399-light-pinebook-power-and-standby-leds-during-early-boot.patch"
        "0004-phy-rockchip-inno-usb2-fix-hang-when-multiple-controllers-exit.patch"
        "0005-Reduce-bootdelay-to-1.patch"
        "0006-dts-rockchip-rk3399-enable-emmc-phy-for-spl.patch"
        "0007-mmc-sdhci-allow-disabling-sdma-in-spl.patch")
sha256sums=('cde723e19262e646f2670d25e5ec4b1b368490de950d4e26275a988c36df0bd4'
            '4e59f02ccb042d5d18c89c849701b96e6cf4b788709564405354b5d313d173f7'
            'c3ea09a18b766a3ce0728234b097b29e2ed610c7f04b138b7fba42e118a7ae33'
            '017d33aac55f8a5ed22170c97b4792ba755a4dad04f6c0cdd85119bbc81e87b3'
            'd111657a85901cb43fe21cd88709945bc27532d3d5bb3f1e79e78da130208212'
            '7652c1bc18ea4266d1fcccb4532690abd315c1079730367b347d3ac10e03527b'
            'e6d08651dc60319eef83c56a3360e62ba22e589632a56fb30f7dc14666979734'
            '54a6b35760f86023ad46e3febd5f97d32f0fcdba9b7775c531b452bcaf6d5eef'
            'cc8ab02844db2dc54a222dbbb4e2666edc6d9fa9d2e5228e8a3606695c2b8bf5')

prepare() {
  cd "${srcdir}/u-boot-${pkgver}"

  local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    echo "Applying patch $src..."
    patch -Np1 < "../$src"
  done
}

build() {
  unset CFLAGS CXXFLAGS CPPFLAGS LDFLAGS

  cd "${srcdir}/trusted-firmware-a-${_tfaver}"
  make PLAT=rk3399

  cd "${srcdir}/u-boot-${pkgver}"
  make pinebook-pro-rk3399_defconfig
  
  make BL31="${srcdir}/trusted-firmware-a-${_tfaver}/build/rk3399/release/bl31/bl31.elf"
}

package() {
  cd "${srcdir}/u-boot-${pkgver}"

  mkdir -p "${pkgdir}/boot/extlinux"
  cp idbloader.img u-boot.itb "${pkgdir}/boot/"
}
