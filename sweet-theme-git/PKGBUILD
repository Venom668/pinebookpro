pkgname=sweet-theme-git
pkgver=r195.64e3457
pkgrel=1
pkgdesc="A dark and modern theme"
arch=(any)
url="https://github.com/EliverLara/Sweet"
license=('GPL3')
depends=('konsole' 'kvantum-qt5' 'kwin' 'plasma-desktop' 'sddm' 'plymouth')
makedepends=('git')
conflicts=('sweet-kde-git' 'aurorae-theme-sweet' 'kvantum-theme-sweet-git' 'sddm-theme-sweet' 'sweet-gtk-theme' 'sweet-cursor-theme-git' 'sweet-wallpapers' 'sweet-folders-icons-git' 'candy-icons-git' 'plasma-splash-sweet-arch-git')
options=('!strip')
source=("git+https://github.com/Venom668/Sweet-theme"
        "git+https://github.com/abrahammurciano/plasma-splash-sweet-arch"
        "git+https://github.com/abrahammurciano/plymouth-theme-sweet-arch"
        "https://raw.githubusercontent.com/pavelxdd/plymouth-theme-sweet-arch/main/sweet-arch/entry.png"
        "https://raw.githubusercontent.com/pavelxdd/plymouth-theme-sweet-arch/main/sweet-arch/dot.png"
        "plymouth.patch")
sha256sums=('SKIP'
            'SKIP'
            'SKIP'
            'a6b52fa6a4d701996c324f06b4090df3c6716b5619044098525c08f65a807454'
            '854fb2fe636df02e27816862b51b3c9bf3d04e3bcd50daff2a391503036b3617'
            '6ed6040e3a74a51722b5b12fd0acc2a3c868bdd37fe2b460387d1107c9d7c869')

pkgver() {
  cd "${srcdir}/Sweet-theme"
  
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

package() {
    # GTK
    cd "${srcdir}/Sweet-theme"
    install -d "${pkgdir}/usr/share/themes/Sweet"
    cp -r assets cinnamon gnome-shell gtk-2.0 gtk-3.0 gtk-4.0 metacity-1 xfwm4 index.theme "${pkgdir}/usr/share/themes/Sweet"

    # Plasma
    cd "${srcdir}/Sweet-theme/kde"
    cp -r aurorae color-schemes konsole plasma sddm Kvantum "${pkgdir}/usr/share/"

    # Icons, wallpapers
    cd "${srcdir}/Sweet-theme"
    cp -r icons wallpapers "${pkgdir}/usr/share/"

    # Splash
    cd "${srcdir}/plasma-splash-sweet-arch"
    cp -r Sweet-Arch "${pkgdir}/usr/share/plasma/look-and-feel/"
    sed -i '3s/Image/Rectangle/; 5s/source:.*/color: "black"/' "${pkgdir}/usr/share/plasma/look-and-feel/Sweet-Arch/contents/splash/Splash.qml"

    # Plymouth
    cd "${srcdir}/plymouth-theme-sweet-arch"
    patch -Np1 < "${srcdir}/plymouth.patch"
    install -d "${pkgdir}/usr/share/plymouth/themes/"
    cp -r sweet-arch "${pkgdir}/usr/share/plymouth/themes/"
    cp "${srcdir}/entry.png" "${pkgdir}/usr/share/plymouth/themes/sweet-arch"
    cp "${srcdir}/dot.png" "${pkgdir}/usr/share/plymouth/themes/sweet-arch"
}
